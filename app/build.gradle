plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "me.omico.currentactivity"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode getVersionCode()
        versionName getVersionName()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.3.0-alpha02"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.core:core-ktx:1.5.0-alpha05"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.0-rc01"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.0-rc01"
    implementation "androidx.lifecycle:lifecycle-service:2.3.0-rc01"
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.2"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.2"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "com.google.android.material:material:1.3.0-beta01"
    implementation "me.weishu:free_reflection:3.0.1"
    implementation "moe.shizuku.privilege:api:5.0.0-alpha03"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.4.21"
}

static int getVersionCode() {
    return readCommand('git rev-list HEAD --count').toInteger()
}

static String getVersionName() {
    return readCommand('git describe --tags --dirty')
}

static def readCommand(String cmd) {
    return cmd.execute().text.trim()
}
